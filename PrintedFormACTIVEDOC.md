Создание печатной формы документа на основании макета **Active Document** с последующим выводом в **Microsoft Word**.

- [x] Используемый документ [__«Расходная накладная»__](https://github.com/EDO123498/my-projects-screenshots/blob/main/PrintedFormACTIVEDOC1.jpg) 

- [x] Создание в макете **закладок** (интересующих для записи параметров)

![](https://github.com/EDO123498/my-projects-screenshots/blob/main/PrintedFormACTIVEDOC2.jpg)

- [x] Содержание команды **«ПечатьРасходнаяНакладнаяWord»** 

```js
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

        ПечатьНаСервере (ПараметрКоманды);

КонецПроцедуры

&НаСервере
Процедура ПечатьНаСервере(СсылкаНаДокумент)
   
	//Получение данных запросом из документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Номер КАК Номер,
		|	РасходнаяНакладная.Дата КАК Дата,
		|	РасходнаяНакладная.Склад КАК Склад,
		|	РасходнаяНакладная.СуммаПоДокументу КАК СуммаПоДокументу,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РасходнаяНакладная.Склад) КАК СкладПредставление
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &СсылкаНаДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РасходнаяНакладнаяСписокНоменклатуры.Номенклатура) КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество,
		|	РасходнаяНакладнаяСписокНоменклатуры.Цена КАК Цена,
		|	РасходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &СсылкаНаДокумент";
	
	Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатДанныеШапки	= МассивРезультатов[0];
	РезультатДанныеТовары	= МассивРезультатов[1];
	
	ВыборкаШапка 	= РезультатДанныеШапки.Выбрать(); 
	ВыборкаТовары	= РезультатДанныеТовары.Выбрать();
	
	ВыборкаШапка.Следующий();
	
	        //Работа с COM-Объектом
	        //Получение макета Active Document
		Макет = Документы.РасходнаяНакладная.ПолучитьМакет("РасходнаяНакладнаяWord");	
		
		//Получение COM-Объекта для программного управления Active Document  
		Word = Макет.Получить();
		
		//Запуск Word (отключение видимости приложения)
		Word.Application.Visible = Ложь;
		
		//Открытие документа для записи
		Doc = Word.Application.Documents(1);
		
		//Активация документа
		Doc.Activate();
		
		//Выбрать закладку "Номер"
		Word.Bookmarks("Номер").Select();
		
		//Установить для закладки "Номер" текст из результата запроса по реквизиту "Номер" 
		Word.Application.Selection.TypeText(ВыборкаШапка.Номер);		
		
		//Аналогично предыдущим двум пунктам заполняются остальные закладки документа Word 
		Word.Bookmarks("Дата").Select();
		Word.Application.Selection.TypeText(Формат(ВыборкаШапка.Дата, "ДЛФ=DD"));
		
		Word.Bookmarks("Дата1").Select();
		Word.Application.Selection.TypeText(Формат(ТекущаяДата(), "ДЛФ=DDT"));				
		
		Word.Bookmarks("Склад").Select();
		Word.Application.Selection.TypeText(ВыборкаШапка.СкладПредставление);
				
		Word.Bookmarks("СуммаИтого").Select();
		Word.Application.Selection.TypeText(ВыборкаШапка.СуммаПоДокументу);
		
		Word.Bookmarks("Итого").Select();
		Word.Application.Selection.TypeText(ВыборкаШапка.СуммаПоДокументу);
		
		Word.Bookmarks("СуммаИтогоПрописью").Select();
		Word.Application.Selection.TypeText(ЧислоПрописью(
                                ВыборкаШапка.СуммаПоДокументу, , "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"));
				
		//Вычисление НДС
		НДС = ВыборкаШапка.СуммаПоДокументу * 0.18;
		
		Word.Bookmarks("НДС").Select();
		Word.Application.Selection.TypeText(НДС);
		
		Word.Bookmarks("НДС1").Select();
		Word.Application.Selection.TypeText(НДС);
		
		Word.Bookmarks("СуммаНДСПрописью").Select();
		Word.Application.Selection.TypeText(ЧислоПрописью(
                                НДС, , "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"));
		
		//Заполнение табличной части
		//Определение таблицы в документе (первая в документе)
		Таблица = Word.Tables.Item(1);  	
		
		//Заполнение строк таблицы по соответствующим колонкам (без учета шапки таблицы - строка 1) 
		НомерСтроки = 2;
		КоличествоИтого = 0;
		Пока ВыборкаТовары.Следующий() Цикл
			Row = Таблица.Rows.Add(Таблица.Rows(НомерСтроки));
			Row.Cells(1).Range.Text = ВыборкаТовары.НомерСтроки;
			Row.Cells(2).Range.Text = ВыборкаТовары.Номенклатура;
			Row.Cells(3).Range.Text = ("шт.");
			Row.Cells(4).Range.Text = ВыборкаТовары.Цена;
			Row.Cells(5).Range.Text = ВыборкаТовары.Количество;
			Row.Cells(6).Range.Text = ВыборкаТовары.Сумма;
			НомерСтроки = НомерСтроки + 1;
		        КоличествоИтого = КоличествоИтого + ВыборкаТовары.Количество; 
		КонецЦикла;
				
		Word.Bookmarks("КоличествоИтого").Select();
		Word.Application.Selection.TypeText(ЧислоПрописью(
                                КоличествоИтого, , "наименование, наименования, наименований, с, , , , , 0"));
		
		//Удаление пустой строки таблицы
		Таблица.Rows(НомерСтроки).Delete();
					
		//Включение видимости приложения Word
		Word.Application.Visible = Истина;
		
КонецПроцедуры 
```
- [x] Полученный __результат__

![](https://github.com/EDO123498/my-projects-screenshots/blob/main/PrintedFormACTIVEDOC3.jpg)


