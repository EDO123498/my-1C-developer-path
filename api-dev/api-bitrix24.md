- [x] step name
```js
Функция ПолучитьТокен()
	Возврат Константы.тснСайтТокенДоступа.Получить();
КонецФункции
```
```js
Функция ВызватьМетодAPI_POST_Pictures(пПараметры) Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("Сервер", "host");
	Настройки.Вставить("Порт", 443);
	Настройки.Вставить("URL", "pictures");
	СтрокаПараметров = "?xml_id=" + пПараметры[0].GUID;
	
	Соединение = Новый HTTPСоединение(Настройки.Сервер, Настройки.Порт,,,,, Новый ЗащищенноеСоединениеOpenSSL());	
	
	HTTPЗапрос = Новый HTTPЗапрос("/api/" + Настройки.URL + "/" + СтрокаПараметров);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ПолучитьТокен());
	
	HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(пПараметры[0].FILE);
	Ответ = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	
	Если Ответ.КодСостояния <> 200 Тогда    
	        ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("Ошибка запроса к сайту API_POST_Pictures",
                                                              УровеньЖурналаРегистрации.Ошибка,,
                                                              СтрокаПараметров,
                                                              Ответ.ПолучитьТелоКакСтроку());
	КонецЕсли;
	
	Результат = Новый Структура("Успешно, Результат");
	
	Если Ответ.КодСостояния = 200 Тогда
                СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку();
                Результат.Успешно = Истина;
                Результат.Результат = СтрокуВСтруктуру(СтрокаОтвета);
	Иначе
		Результат.Успешно = Ложь;
		Попытка
			СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку();
			Результат.Результат = СтрокуВСтруктуру(СтрокаОтвета);
		Исключение
			Результат.Результат = Новый Структура("Ошибка", ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;		

	Возврат Результат;
		
КонецФункции
```
```js
Функция ВызватьМетодAPI_POST(пПараметры) Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("Сервер", "host");
	Настройки.Вставить("Порт", 443);
	Настройки.Вставить("URL", "products");
	
	Соединение = Новый HTTPСоединение(Настройки.Сервер, Настройки.Порт,,,,, Новый ЗащищенноеСоединениеOpenSSL());	
	
	HTTPЗапрос = Новый HTTPЗапрос("/api/" + Настройки.URL + "/");
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ПолучитьТокен());
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, пПараметры);
	СтрокаJS = ЗаписьJSON.Закрыть();    
	
	HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаJS);
	Ответ = Соединение.ОтправитьДляОбработки(HTTPЗапрос);   

	Если Ответ.КодСостояния <> 200 Тогда
		ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("Ошибка запроса к сайту",
                                                              УровеньЖурналаРегистрации.Ошибка,,
                                                              СтрокаJS,
                                                              Ответ.ПолучитьТелоКакСтроку());
	КонецЕсли;
		
	Результат = Новый Структура("Успешно, Результат");
	
	Если Ответ.КодСостояния = 200 Тогда
		СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку();
		Результат.Успешно = Истина;
		Результат.Результат = СтрокуВСтруктуру(СтрокаОтвета);
	Иначе
		Результат.Успешно = Ложь;
		Попытка
			СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку();
			Результат.Результат = СтрокуВСтруктуру(СтрокаОтвета);
		Исключение
			Результат.Результат = Новый Структура("Ошибка", ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;		

	Возврат Результат;
				
КонецФункции
```
```js
Функция ВызватьМетодAPI_GET() Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("Сервер", "host");
	Настройки.Вставить("Порт", 443);
	Настройки.Вставить("URL", "orders");
	
	Соединение = Новый HTTPСоединение(Настройки.Сервер, Настройки.Порт,,,,, Новый ЗащищенноеСоединениеOpenSSL()); 
	СтрокаПараметров = "modified_since=" + Формат(ТекущаяДата() - 86400,  "ДФ=yyyy-MM-dd") + "T23:00:00";
	URL = Настройки.URL + "/?" + СтрокаПараметров;
	                    
	HTTPЗапрос = Новый HTTPЗапрос("/api/" + URL);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ПолучитьТокен());
	
	Ответ = Соединение.Получить(HTTPЗапрос);
	Результат = Новый Структура("Успешно, Результат");
	
	Если Ответ.КодСостояния = 200 Тогда
		СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку();
		Результат.Успешно = Истина;
		Результат.Результат = тснСайтВспомогательныеФункции.СтрокуВСтруктуру(СтрокаОтвета);
	Иначе
		Результат.Успешно = Ложь;
		Попытка
			СтрокаОтвета = Ответ.ПолучитьТелоКакСтроку();
			Результат.Результат = тснСайтВспомогательныеФункции.СтрокуВСтруктуру(СтрокаОтвета);
		Исключение
			Результат.Результат = Новый Структура("Ошибка", ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;		

	Возврат Результат;
	
КонецФункции
```
```js
Функция СтрокуВСтруктуру(Строка) Экспорт
	Попытка
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(Строка);
		Результат = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	Исключение
		Результат = Новый Структура();
	КонецПопытки;
	Возврат Результат;
КонецФункции 
```
```js                                                                        
Функция СтруктуруВСтроку(Структура) Экспорт
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
	Возврат ЗаписьJSON.Закрыть();
КонецФункции
```
