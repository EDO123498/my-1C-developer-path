```js
Функция ЗаписьДокумента_1С(СтруктураJSON) Экспорт     
	//выгрузка регистра в таблицу значений	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тснРегистрСоответствияСайт.Сайт КАК Сайт,
	|	тснРегистрСоответствияСайт.База1С КАК База1С
	|ИЗ
	|	РегистрСведений.тснРегистрСоответствияСайт КАК тснРегистрСоответствияСайт";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаСоответствий = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.Прямой);	
	
	Данные = СтруктураJSON.Результат.data.items;
	
	//Получение ID заказов из регистра
	ТипЗаказКлиента = Перечисления.тснТипСоответствияБитрикс["ЗаказКлиента"];
	МассивID = Новый Массив;
	Для Каждого лЭлемент Из Данные Цикл 
		МассивID.Добавить(лЭлемент.ID);	
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тснРегистрСоответствийБитрикс.ID КАК ID,
	|	тснРегистрСоответствийБитрикс.Объект КАК Ссылка
	|ИЗ
	|	РегистрСведений.тснРегистрСоответствийБитрикс КАК тснРегистрСоответствийБитрикс
	|ГДЕ
	|	тснРегистрСоответствийБитрикс.ID В(&ID)
	|	И тснРегистрСоответствийБитрикс.Тип.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ID", МассивID);
	Запрос.УстановитьПараметр("Ссылка", ТипЗаказКлиента);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МассивID = Новый Массив;
	ID = Новый Соответствие;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		МассивID.Добавить(ВыборкаДетальныеЗаписи.ID);
		ID.Вставить(ВыборкаДетальныеЗаписи.ID, ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	//Получение неизменяемых данных
	лОрганизация =
            Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("c776b284-0469-11e1-a10e-002618f1e4a1"));
	лСклад =
            Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор("45279a19-a1b2-11e1-b648-4cedde064d5a"));
	лСтавкаНДС = Справочники.СтавкиНДС.БезНДС;	
	лВалюта = Справочники.Валюты.НайтиПоКоду("643"); 
	лДоставкаЗаказа = Справочники.Номенклатура.НайтиПоКоду("00000002793");
	
	Для Каждого лЭлемент Из Данные Цикл 
		Если лЭлемент["CANCELED"] = "N" И Дата(лЭлемент["DATE_INSERT"]) >= '20231005230000' Тогда
			НомерЗаказа = МассивID.Найти(лЭлемент.ID);	
			Если НомерЗаказа = Неопределено Тогда 
				ДокументОбъект = Документы.ЗаказКлиента.СоздатьДокумент();
			Иначе
				ДокументСсылка = ID.Получить(лЭлемент.ID); 	
				ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
				Если ПроверитьСвязанныеДокументы_1С(ДокументОбъект.Ссылка) Тогда
					//Заполнение Регистра ошибок
					НаборЗаписей = РегистрыСведений.тснРегистрОшибокСайт.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Объект.Установить(ДокументОбъект.Ссылка);
					НоваяЗапись = НаборЗаписей.Добавить();
					НоваяЗапись.Объект = ДокументОбъект.Ссылка;
					НоваяЗапись.ОписаниеОшибки = "Ошибка перезаписи документа: "
                                        + ДокументОбъект.Ссылка
                                        + ". На основании документа существует реализация.";
					НаборЗаписей.Записать();
					Продолжить;	
				КонецЕсли;
			КонецЕсли;
			ДокументОбъект.Дата = лЭлемент.DATE_INSERT;
			ДокументОбъект.Проведен = Ложь;
			ДокументОбъект.ПометкаУдаления = Ложь;	
			ДокументОбъект.СпособДоставки = Перечисления.СпособыДоставки.ДоКлиента;
			ДокументОбъект.Валюта = лВалюта;					
			ДокументОбъект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказам;
			ДокументОбъект.ДатаОтгрузки = ?(лЭлемент.USER_FIELDS.F_DELIVERY_TIME = "",
                                      лЭлемент.DATE_INSERT,
                                      лЭлемент.USER_FIELDS.F_DELIVERY_TIME + " 00:00:00"); 
			ДокументОбъект.Комментарий = "[Номер документа на сайте: " + лЭлемент.ID + "]"
                                    + ?(ЗначениеЗаполнено(лЭлемент.COMMENTS), Символы.ПС
                                    + "[Комментарий менеджера: " + лЭлемент.COMMENTS + "]", "");
			ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
			ДокументОбъект.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
			ДокументОбъект.Организация =  лОрганизация;
			ДокументОбъект.Склад = лСклад;
			ДокументОбъект.ЦенаВключаетНДС = Ложь;
			ДокументОбъект.НеОтгружатьЧастями = Истина;
			ДокументОбъект.Статус = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
			ДокументОбъект.ДатаСогласования = лЭлемент.DATE_INSERT;
			ДокументОбъект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
			ДокументОбъект.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();		
			ДанныеКонтрагента = ПолучитьПартнера_1C(лЭлемент.USER_FIELDS, лЭлемент.USER_ID);
			ДокументОбъект.Контрагент = ДанныеКонтрагента["Контрагент"]; 
			ДокументОбъект.Договор = ДанныеКонтрагента["Договор"];
			ДокументОбъект.Партнер = ДокументОбъект.Контрагент.Партнер;
			
			ДокументОбъект.Товары.Очистить();
			лСуммаДокумента = 0;
			Для Каждого ЗаписьТЧ Из лЭлемент.BASKET_ITEMS Цикл
				
				НоваяСтрокаТЧ = ДокументОбъект.Товары.Добавить();
				НоваяСтрокаТЧ.ДатаОтгрузки = лЭлемент.DATE_INSERT;
				НоваяСтрокаТЧ.Количество = ЗаписьТЧ.QUANTITY;
				НоваяСтрокаТЧ.КоличествоУпаковок = ЗаписьТЧ.QUANTITY;
				НоваяСтрокаТЧ.Номенклатура =
                                Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(ЗаписьТЧ.PRODUCT_XML_ID));
				НоваяСтрокаТЧ.СтавкаНДС = лСтавкаНДС;
				НоваяСтрокаТЧ.ВидЦены = лВидЦены;
				НоваяСтрокаТЧ.Цена = ЗаписьТЧ.PRICE;
				НоваяСтрокаТЧ.Сумма = НоваяСтрокаТЧ.Цена * НоваяСтрокаТЧ.Количество;
				НоваяСтрокаТЧ.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.НеТребуется;
				НоваяСтрокаТЧ.СуммаСНДС	 = НоваяСтрокаТЧ.Сумма;			
				лСуммаДокумента = лСуммаДокумента + НоваяСтрокаТЧ.Сумма;
				
			КонецЦикла;
			//добавление услуги доставки в ТЧ
			Если НЕ Число(лЭлемент["PRICE_DELIVERY"]) = 0 Тогда 
				НоваяСтрокаТЧ = ДокументОбъект.Товары.Добавить();
				НоваяСтрокаТЧ.ДатаОтгрузки = лЭлемент.DATE_INSERT;
				НоваяСтрокаТЧ.Количество = 1;
				НоваяСтрокаТЧ.КоличествоУпаковок = 1;
				НоваяСтрокаТЧ.Номенклатура = лДоставкаЗаказа;
				НоваяСтрокаТЧ.СтавкаНДС = лСтавкаНДС;
				НоваяСтрокаТЧ.ВидЦены = лВидЦены;
				НоваяСтрокаТЧ.Цена = лЭлемент["PRICE_DELIVERY"];
				НоваяСтрокаТЧ.Сумма = НоваяСтрокаТЧ.Цена * НоваяСтрокаТЧ.Количество;
				НоваяСтрокаТЧ.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.НеТребуется;
				НоваяСтрокаТЧ.СуммаСНДС	 = НоваяСтрокаТЧ.Сумма;			
				лСуммаДокумента = лСуммаДокумента + НоваяСтрокаТЧ.Сумма;	
			КонецЕсли;
			ДокументОбъект.СуммаДокумента = лСуммаДокумента;
	
			//заполнение этапов графика оплаты
			ДокументОбъект.ЭтапыГрафикаОплаты.Очистить(); //комментим, так как заказ не перезаписываем
			НоваяСтрокаТЧ = ДокументОбъект.ЭтапыГрафикаОплаты.Добавить();
			НоваяСтрокаТЧ.ВариантОплаты = Перечисления.ВариантыКонтроляОплатыКлиентом.КредитСдвиг;
			НоваяСтрокаТЧ.ДатаПлатежа = ДокументОбъект.ДатаОтгрузки;
			НоваяСтрокаТЧ.ПроцентПлатежа = 100;
			НоваяСтрокаТЧ.СуммаПлатежа = ДокументОбъект.СуммаДокумента;
			НоваяСтрокаТЧ.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыОтгрузки;
			НоваяСтрокаТЧ.Сдвиг = 0;
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			//запись в регистр соответствия ID 
			НаборЗаписей = РегистрыСведений.тснРегистрСоответствийБитрикс.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДокументОбъект.Ссылка);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Объект = ДокументОбъект.Ссылка;
			НоваяЗапись.ID = лЭлемент.ID;
			НоваяЗапись.Тип = ТипЗаказКлиента;
			НаборЗаписей.Записать();
			
			//добавление дополнительных сведений в регистр и заказ клиента
			НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДокументОбъект.Ссылка);
			СвойстваЗаказа = лЭлемент.USER_FIELDS;
			//дополнительные поля
			СвойстваЗаказа.Вставить("FULLADDR_1C", СвойстваЗаказа["CITY"]
                                              + ", "
                                              + СвойстваЗаказа["FULLADDR"]
                                              + ", "
                                              + СвойстваЗаказа["HOUSE"]);
			Попытка
				СвойстваЗаказа.Вставить("DELIVERY_NAME", лЭлемент.SHIPMENT["DELIVERY_NAME"]);
			Исключение
			КонецПопытки;
			СвойстваЗаказа.Вставить("PAYMENT_SYSTEM_NAME", лЭлемент.PAYMENT["PAYMENT_SYSTEM_NAME"]);
			СвойстваЗаказа.Вставить("PAID", ?(лЭлемент.PAYMENT["PAID"], "Оплачен", "Не оплачен"));
			Для Каждого лЭлемент Из СвойстваЗаказа Цикл 
				Если лЭлемент.Ключ = "FIO" Тогда 
					ПараметрыОтбора = Новый Структура();
					ПараметрыОтбора.Вставить("Сайт", лЭлемент.Ключ);
					ИспользуемыйЗаголовок = ТаблицаСоответствий.НайтиСтроки(ПараметрыОтбора);	
					Если НЕ ИспользуемыйЗаголовок.Количество() = 0 Тогда
						Для Каждого Строка Из ИспользуемыйЗаголовок Цикл 
							НоваяЗапись = НаборЗаписей.Добавить();
							НоваяЗапись.Объект 	= ДокументОбъект.Ссылка;
							НоваяЗапись.Свойство = Строка.База1С;
							НоваяЗапись.Значение = лЭлемент.Значение;		
						КонецЦикла;
					КонецЕсли;
				Иначе 
					ИспользуемыйЗаголовок = ТаблицаСоответствий.Найти(лЭлемент.Ключ, "Сайт");
					Если НЕ ИспользуемыйЗаголовок = Неопределено Тогда 
						НоваяЗапись = НаборЗаписей.Добавить();
						НоваяЗапись.Объект 	= ДокументОбъект.Ссылка;
						НоваяЗапись.Свойство = ИспользуемыйЗаголовок.База1С;
						НоваяЗапись.Значение = лЭлемент.Значение;					
					Иначе 
						Продолжить;
					КонецЕсли;
				КонецЕсли;	
			КонецЦикла;						
			НаборЗаписей.Записать();
		//перезапись отмененных заказов, которые уже существуют в системе 	
		ИначеЕсли лЭлемент["CANCELED"] = "Y" И Дата(лЭлемент["DATE_INSERT"]) >= '20231005230000' Тогда
			НомерЗаказа = МассивID.Найти(лЭлемент.ID);	
			Если НомерЗаказа = Неопределено Тогда 
				Продолжить;
			Иначе
				ДокументСсылка = ID.Получить(лЭлемент.ID); 	
				ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			КонецЕсли;
			ДокументОбъект.Проведен = Ложь;
			ДокументОбъект.ПометкаУдаления = Истина;
			ДокументОбъект.Комментарий = ДокументОбъект.Комментарий
                                    + ?(ЗначениеЗаполнено(лЭлемент.REASON_CANCELED), Символы.ПС
                                    + "[Комментарий отмены: "
                                    + лЭлемент.REASON_CANCELED + "]", ""); 
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
	КонецЦикла;
	
КонецФункции
```
```js
Функция ПолучитьПартнера_1C(пКонтактнаяИнформация, пID)
	
	лТипыКонтактнойИнформации = Новый Структура;
	лТипыКонтактнойИнформации.Вставить("Телефон", пКонтактнаяИнформация.PHONE);
	лТипыКонтактнойИнформации.Вставить("Адрес", пКонтактнаяИнформация.CITY
						+ ", "
						+ пКонтактнаяИнформация.FULLADDR
						+ ", "
						+ пКонтактнаяИнформация.HOUSE);
	лТипыКонтактнойИнформации.Вставить("АдресЭлектроннойПочты", пКонтактнаяИнформация.EMAIL);
	лВидыКонтактнойИнформации = Новый Структура;
	лВидыКонтактнойИнформации.Вставить("Телефон", "Телефон");
	лВидыКонтактнойИнформации.Вставить("Адрес", "Адрес");
	лВидыКонтактнойИнформации.Вставить("АдресЭлектроннойПочты", "Электронная почта");
	ТипКонтрагент = Перечисления.тснТипСоответствияБитрикс["Контрагент"];
	
	//Поиск Контрагента по ID в регистре
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тснРегистрСоответствийБитрикс.Объект КАК Ссылка
		|ИЗ
		|	РегистрСведений.тснРегистрСоответствийБитрикс КАК тснРегистрСоответствийБитрикс
		|ГДЕ
		|	тснРегистрСоответствийБитрикс.ID = &ID
		|	И тснРегистрСоответствийБитрикс.Тип.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ID", пID);
	Запрос.УстановитьПараметр("Ссылка", ТипКонтрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	
	Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда 
		//Поиск Контрагента по номеру телефона в регистре
		МассивТелефонов = Новый Массив;
		МассивТелефонов.Добавить(
		тснСайтВспомогательныеФункции.СтандартизироватьНомерТелефона(пКонтактнаяИнформация.PHONE));
		Если ЗначениеЗаполнено(пКонтактнаяИнформация.ALTPHONE) Тогда 
			МассивТелефонов.Добавить(
			тснСайтВспомогательныеФункции.СтандартизироватьНомерТелефона(пКонтактнаяИнформация.ALTPHONE));
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	тснРегистрТелефоныКонтрагентов.Контрагент КАК Ссылка
		|ИЗ
		|	РегистрСведений.тснРегистрТелефоныКонтрагентов КАК тснРегистрТелефоныКонтрагентов
		|ГДЕ
		|	тснРегистрТелефоныКонтрагентов.Телефон В (&Массив)";
		
		Запрос.УстановитьПараметр("Массив", МассивТелефонов);
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
		
		Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда 
			СправочникОбъект = Справочники.Контрагенты.СоздатьЭлемент();	
		Иначе 
			лКонтрагентСсылка = ВыборкаДетальныеЗаписи[0].Ссылка;
			СправочникОбъект = лКонтрагентСсылка.ПолучитьОбъект();
			//перезаписываем ID регистр
			//запись в регистр соответствия ID 
			НаборЗаписей = РегистрыСведений.тснРегистрСоответствийБитрикс.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(лКонтрагентСсылка);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Объект = лКонтрагентСсылка;
			НоваяЗапись.ID = пID;
			НоваяЗапись.Тип = ТипКонтрагент;
			НаборЗаписей.Записать();
		КонецЕсли;		
	Иначе 
		лКонтрагентСсылка = ВыборкаДетальныеЗаписи[0].Ссылка;
		СправочникОбъект = лКонтрагентСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	СправочникОбъект.Наименование = ?(ЗначениеЗаполнено(пКонтактнаяИнформация.FIO_CONTR),
					пКонтактнаяИнформация.FIO_CONTR,
					пКонтактнаяИнформация.FIO);
	СправочникОбъект.НаименованиеПолное = ?(ЗначениеЗаполнено(пКонтактнаяИнформация.FIO_CONTR),
						пКонтактнаяИнформация.FIO_CONTR,
						пКонтактнаяИнформация.FIO);
	СправочникОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо["ФизическоеЛицо"];
	СправочникОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо["ФизЛицо"];
	ПартнерОбъект = ?(СправочникОбъект.Партнер.Пустая(),
			Справочники.Партнеры.СоздатьЭлемент(),
			СправочникОбъект.Партнер.ПолучитьОбъект());
	ПартнерОбъект.Наименование = СправочникОбъект.Наименование;
	ПартнерОбъект.НаименованиеПолное = СправочникОбъект.НаименованиеПолное;
	ПартнерОбъект.УстановитьНовыйКод();						
	ПартнерОбъект.Клиент = Истина; 
	ПартнерОбъект.КонтактнаяИнформация.Очистить();
	Для Каждого Строка Из лТипыКонтактнойИнформации Цикл
		лСтрокаТЧ = ПартнерОбъект.КонтактнаяИнформация.Добавить();
		лСтрокаТЧ.Тип = Перечисления.ТипыКонтактнойИнформации[Строка.Ключ];
		лСтрокаТЧ.Вид =
		Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию(
		лВидыКонтактнойИнформации[Строка.Ключ],
		Истина,
		Справочники.ВидыКонтактнойИнформации.СправочникПартнеры);
		Если Строка.Ключ = "Телефон" Тогда  
			лСтрокаТЧ.Представление =
			тснСайтВспомогательныеФункции.СтандартизироватьНомерТелефона(Строка.Значение);       
		Иначе
			лСтрокаТЧ.Представление = Строка.Значение;
		КонецЕсли;
		//добавление дополнительного номера
		Если ЗначениеЗаполнено(пКонтактнаяИнформация.ALTPHONE) И Строка.Ключ = "Телефон" Тогда 
			лСтрокаТЧ = ПартнерОбъект.КонтактнаяИнформация.Добавить();
			лСтрокаТЧ.Тип = Перечисления.ТипыКонтактнойИнформации[Строка.Ключ];
			лСтрокаТЧ.Вид =
			Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию(
			лВидыКонтактнойИнформации[Строка.Ключ],
			Истина,
			Справочники.ВидыКонтактнойИнформации.СправочникПартнеры);
			лСтрокаТЧ.Представление =
			тснСайтВспомогательныеФункции.СтандартизироватьНомерТелефона(пКонтактнаяИнформация.ALTPHONE);	
		КонецЕсли;
	КонецЦикла;
		
	ПартнерОбъект.Записать();
	СправочникОбъект.Партнер = ПартнерОбъект.Ссылка;
	СправочникОбъект.Записать();	
	//запись в регистр соответствия ID 
	НаборЗаписей = РегистрыСведений.тснРегистрСоответствийБитрикс.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(СправочникОбъект.Ссылка);
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Объект = СправочникОбъект.Ссылка;
	НоваяЗапись.ID = пID;
	НоваяЗапись.Тип = ТипКонтрагент;
	НаборЗаписей.Записать();
	//запись в регистр соответствия 
	
	ДоговорКонтрагента = ПолучитьДоговор_1С(ПартнерОбъект.Ссылка, СправочникОбъект.Ссылка); 
	ДанныеКонтрагента = Новый Структура;
	ДанныеКонтрагента.Вставить("Контрагент", СправочникОбъект.Ссылка);
	ДанныеКонтрагента.Вставить("Договор", ДоговорКонтрагента);
	Возврат ДанныеКонтрагента;
		
КонецФункции
```

Функция ПолучитьДоговор_1С(пПартнер, пКонтрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", пКонтрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	Если НЕ ВыборкаДетальныеЗаписи.Количество() = 0 Тогда 
		ДоговорКонтрагента = ВыборкаДетальныеЗаписи[0].Ссылка;	
		Возврат ДоговорКонтрагента;
	Иначе 
		лОрганизация = Справочники.Организации.НайтиПоНаименованию("ИП Владыкин Д.Ю.", Истина);
		лВалюта = Справочники.Валюты.НайтиПоКоду("643"); 
		
		СправочникОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		СправочникОбъект.Наименование = "Договор WEB - сайт";
		СправочникОбъект.НаименованиеДляПечати = "Договор WEB - сайт";
		//СправочникОбъект.Номер = "";
		СправочникОбъект.Организация = лОрганизация;
		СправочникОбъект.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем;
		СправочникОбъект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Действует;
		СправочникОбъект.ВалютаВзаиморасчетов = лВалюта;
		СправочникОбъект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказам;
		СправочникОбъект.Партнер = пПартнер;
		СправочникОбъект.Контрагент = пКонтрагент;
		СправочникОбъект.НалогообложениеНДСОпределяетсяВДокументе = Истина;
		СправочникОбъект.Записать();
		Возврат СправочникОбъект.Ссылка;
	КонецЕсли;
	////ДоговорыКонтрагентов
	//ДоговорыКонтрагентов = Данные.ДоговорыКонтрагентов;
	//Для Каждого лСправочник Из ДоговорыКонтрагентов Цикл
	//	
	//	Если лСправочник.ЭтоГруппа Тогда
	//		Продолжить;	
	//	КонецЕсли;
	//	
	//	СсылкаНаСправочник = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(Новый УникальныйИдентификатор(лСправочник.Идентификатор));
	//	СправочникОбъект = СсылкаНаСправочник.ПолучитьОбъект();
	//	Если СправочникОбъект = Неопределено Тогда
	//		СправочникОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент(); 
	//		СправочникОбъект.УстановитьСсылкуНового(СсылкаНаСправочник);
	//	КонецЕсли;     
	//	
	//	//СправочникОбъект.ОбменДанными.Загрузка = Истина;
	//		
	//	СправочникОбъект.Наименование					= лСправочник.Наименование; 
	//	СправочникОбъект.ПометкаУдаления 				= лСправочник.ПометкаУдаления;				
	//	СправочникОбъект.ВалютаВзаиморасчетов			= Справочники.Валюты.НайтиПоКоду(лСправочник.ВалютаВзаиморасчетов); 
	//	СправочникОбъект.Номер	        				= лСправочник.Код;
	//	//СправочникОбъект.ДоступноДляЗакупки         	= НЕ лСправочник.ПометкаУдаления;
	//	//СправочникОбъект.СтатьяДвиженияДенежныхСредств 	= Справочники.СтатьиДвиженияДенежныхСредств.. .НайтиПоНаименованию(лСправочник.ОсновнаяСтатьяДвиженияДенежныхСредств);
	//	СправочникОбъект.Контрагент						= Справочники.Контрагенты.ПолучитьСсылку(Новый УникальныйИдентификатор(лСправочник.Владелец));
	//	Если ЗначениеЗаполнено(СправочникОбъект.Контрагент) Тогда
	//		СправочникОбъект.Партнер					= СправочникОбъект.Контрагент.Партнер;		
	//	КонецЕсли;
	//	СправочникОбъект.Согласован         			= Истина;
	//	СправочникОбъект.ОграничиватьСуммуЗадолженности	= (лСправочник.ДопустимаяСуммаЗадолженности <> 0);
	//	СправочникОбъект.НаименованиеДляПечати          = лСправочник.Наименование;
	//	СправочникОбъект.ДатаОкончанияДействия          = XMLЗначение(Тип("Дата"), лСправочник.СрокДействия);
	//	Если лСправочник.ВидДоговора = "СПоставщиком" Тогда
	//		СправочникОбъект.ТипДоговора				= Перечисления.ТипыДоговоров.СПоставщиком;
	//	Иначе
	//		СправочникОбъект.ТипДоговора				= Перечисления.ТипыДоговоров.СПокупателем;
	//	КонецЕсли;
	//	
	//	//ADD 19/05/2023
	//	Если лСправочник.ВедениеВзаиморасчетов = "ПоДоговоруВЦелом" Тогда
	//		СправочникОбъект.ПорядокРасчетов			= Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов;
	//	ИначеЕсли лСправочник.ВедениеВзаиморасчетов = "ПоЗаказам" Тогда 
	//		СправочникОбъект.ПорядокРасчетов			= Перечисления.ПорядокРасчетов.ПоЗаказам; 
	//	ИначеЕсли лСправочник.ВедениеВзаиморасчетов = "ПоСчетам" Тогда 
	//		СправочникОбъект.ПорядокРасчетов			= Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов; 
	//	КонецЕсли;
	//	//END ADD 		
	//
	//	//Если СтрНайти(лСправочник.ВидАгентскогоДоговора,"Аренда") <> 0 Тогда
	//	//	СправочникОбъект.ВидАгентскогоДоговора		= Перечисления.ВидыАгентскихДоговоров.Аренда;
	//	//КонецЕсли;	
	//	СправочникОбъект.ДопустимаяСуммаЗадолженности 	= лСправочник.ДопустимаяСуммаЗадолженности;
	//	СправочникОбъект.Дата	                        = лСправочник.Дата;
	//	СправочникОбъект.ДатаНачалаДействия				= лСправочник.Дата;
	//	СправочникОбъект.Организация					= Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(лСправочник.Организация));
	//	СправочникОбъект.НалогообложениеНДСОпределяетсяВДокументе = Истина;
	//			
	//	СправочникОбъект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Действует; 
	//	Если (ЗначениеЗаполнено(СправочникОбъект.ДатаОкончанияДействия) И СправочникОбъект.ДатаОкончанияДействия < ТекущаяДата()) ИЛИ СправочникОбъект.ПометкаУдаления Тогда
	//		СправочникОбъект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт; 
	//	КонецЕсли;
	//	СправочникОбъект.Записать();
	//	
	//КонецЦикла;	
	//Сообщить("Справочник ДоговорыКонтрагентов Выгружен");		
КонецФункции

Функция ПроверитьСвязанныеДокументы_1С(пЗаказКлиента)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.ЗаказКлиента = &ЗаказКлиента";
	
	Запрос.УстановитьПараметр("ЗаказКлиента", пЗаказКлиента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	Если НЕ ВыборкаДетальныеЗаписи.Количество() = 0 Тогда 
		Возврат Истина;			
	КонецЕсли;
	Возврат Ложь;

КонецФункции
