Создание печатной формы документа на основании шаблона из Excel-файла, макетом для создания является **табличный** документ.

- [x] Используемый [__Excel-файл__](https://github.com/EDO123498/my-projects-screenshots/blob/main/WarrantyCard.xlsx) 

- [x] Создание в макете вертикальных областей таблицы и параметров

```js
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
  	  ТабДок = Новый ТабличныйДокумент;
	  
	  ПечатьНаСервере(ТабДок, ПараметрКоманды);
	  
	  ТабДок.Показать("Гарантийный талон");
КонецПроцедуры

&НаСервере
Процедура ПечатьНаСервере(Документ, СсылкаНаДокумент)
	
	//Получение данных запросом из документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Номер КАК Номер,
		|	РасходнаяНакладная.Дата КАК Дата
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &СсылкаНаДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки КАК НомерСтроки,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Наименование КАК НаименованиеТовара,
		|	РасходнаяНакладнаяСписокНоменклатуры.Цена КАК Цена,
		|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &СсылкаНаДокумент";
	
	Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	РезультатДанныеШапки 	 = МассивРезультатовЗапроса[0];
	РезультатДанныеТовары 	 = МассивРезультатовЗапроса[1];
	
	ВыборкаШапка = РезультатДанныеШапки.Выбрать();
	ВыборкаШапка.Следующий();
	
	ВыборкаТовары = РезультатДанныеТовары.Выбрать();
	
	  //Получение макета
	  Макет = Документы.РасходнаяНакладная.ПолучитьМакет("ГарантийныйТалон");
	  
	  //Шапка
	  ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	  ОбластьШапка.Параметры.Номер 	= ВыборкаШапка.Номер;
	  ОбластьШапка.Параметры.Дата	= ВыборкаШапка.Дата;
	  Документ.Вывести (ОбластьШапка);
	  
	  //Шапка таблицы
	  ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	  Документ.Вывести (ОбластьШапкаТаблицы);
	  
	  //Строка товары
	  СуммаВсего = 0;
	  Пока ВыборкаТовары.Следующий() Цикл
	           	   ОбластьСтрокаТовары = Макет.ПолучитьОбласть("СтрокаТовары");
			   ОбластьСтрокаТовары.Параметры.НомерСтроки        = ВыборкаТовары.НомерСтроки;
			   ОбластьСтрокаТовары.Параметры.НаименованиеТовара = ВыборкаТовары.НаименованиеТовара;
			   ОбластьСтрокаТовары.Параметры.Цена 		    = ВыборкаТовары.Цена;
			   ОбластьСтрокаТовары.Параметры.Количество 	    = ВыборкаТовары.Количество;
			   Документ.Вывести (ОбластьСтрокаТовары);
	  	           СуммаВсего = СуммаВсего + ВыборкаТовары.Цена * ВыборкаТовары.Количество; 
	  КонецЦикла;
	  
	  //Подвал таблицы
	  ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	  ОбластьПодвалТаблицы.Параметры.СуммаВсего = СуммаВсего;
	  Документ.Вывести (ОбластьПодвалТаблицы);
	  
	  //Текст соглашения
	  ОбластьТекстСоглашения = Макет.ПолучитьОбласть("ТекстСоглашения");
	  ОбластьТекстСоглашения.Параметры.Дата = Формат (ТекущаяДата(), "ДЛФ=DDT");
	  Документ.Вывести (ОбластьТекстСоглашения);

КонецПроцедуры
```
