Создание печатной формы документа на основании шаблона из Notebook-файла, макетом для создания является **текстовый** документ.

- [x] Используемый [__Notebook-файл__](https://github.com/EDO123498/my-projects-screenshots/blob/main/%D0%A1heque.txt) 

- [x] Создание в макете областей документа и параметров

![](https://github.com/EDO123498/my-projects-screenshots/blob/main/PrintedFormTEXTDOC1.jpg)

- [x] Содержание команды **«ПечатьЧекККМ»** 

```js
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ТекстДок = Новый ТекстовыйДокумент;
	
	ПечатьНаСервере(ТекстДок, ПараметрКоманды);
	
	ТекстДок.Показать("Чек ККМ");
КонецПроцедуры

&НаСервере
Процедура ПечатьНаСервере(ТекстДок, СсылкаНаДокумент)
	
	//Получение данных запросом из документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Номер КАК Номер,
		|	РасходнаяНакладная.Дата КАК Дата,
		|	РасходнаяНакладная.Склад КАК Склад
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &СсылкаНаДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки КАК НомерСтроки,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Наименование КАК НаименованиеТовара,
		|	РасходнаяНакладнаяСписокНоменклатуры.Цена КАК Цена,
		|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &СсылкаНаДокумент";
	
	Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
	
	МассивРезультатовЗапроса  = Запрос.ВыполнитьПакет();
	РезультатДанныеШапки      = МассивРезультатовЗапроса[0];
	РезультатДанныеТовары     = МассивРезультатовЗапроса[1];
	
	ВыборкаШапка = РезультатДанныеШапки.Выбрать();
	ВыборкаШапка.Следующий();
	
	ВыборкаТовары = РезультатДанныеТовары.Выбрать();
	
	//Получение макета текстового документа
	Макет = Документы.РасходнаяНакладная.ПолучитьМакет("ЧекККМ");
	
	//#Область Шапка
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Номер 	= ВыборкаШапка.Номер;
	ОбластьШапка.Параметры.Дата 	= ВыборкаШапка.Дата;
        ОбластьШапка.Параметры.Склад 	= ВыборкаШапка.Склад;
	ТекстДок.Вывести(ОбластьШапка);
	
	//#Область ШапкаТаблицы
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТекстДок.Вывести(ОбластьШапкаТаблицы);
	
	//#Область СтрокаТаблицы
	КоличествоИтого = 0;
	СуммаИтого      = 0;
	Пока ВыборкаТовары.Следующий () Цикл
	        ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	        ОбластьСтрокаТаблицы.Параметры.Номенклатура     = ВыборкаТовары.НаименованиеТовара;
	        ОбластьСтрокаТаблицы.Параметры.Количество       = ВыборкаТовары.Количество;
	        ОбластьСтрокаТаблицы.Параметры.Цена             = ВыборкаТовары.Цена;
		      
                СуммаВсего = ВыборкаТовары.Количество * ВыборкаТовары.Цена; 
          
                ОбластьСтрокаТаблицы.Параметры.Сумма            = СуммаВсего;
                
                КоличествоИтого   = КоличествоИтого + ВыборкаТовары.Количество;
                СуммаИтого        = СуммаИтого + СуммаВсего;
                ТекстДок.Вывести(ОбластьСтрокаТаблицы);
	КонецЦикла;
	    
	//#Область Итоги
	ОбластьИтоги = Макет.ПолучитьОбласть("Итоги");
	ОбластьИтоги.Параметры.КоличествоВсего    = КоличествоИтого;
	ОбластьИтоги.Параметры.СуммаВсего         = СуммаИтого;
	ТекстДок.Вывести(ОбластьИтоги);
    
КонецПроцедуры
```

- [x] Полученный __результат__

![](https://github.com/EDO123498/my-projects-screenshots/blob/main/PrintedFormTEXTDOC2.jpg)

