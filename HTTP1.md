Получение данных о курсе валют с сайта __https://www.nbrb.by/api/exrates/rates?periodicity=0&ondate=2023-02-20__ с последующей записью в регистр сведений.

```js
&НаКлиенте
Процедура GetCourseValue(Команда) 
    GetCourseValue1();
КонецПроцедуры
 
&НаСервере
Процедура GetCourseValue1()
			
	СоответствиеВалют = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Валюты.Код КАК Код,
		|	Валюты.Наименование КАК Наименование,
		|	Валюты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Валюты КАК Валюты
		|ГДЕ
		|	НЕ Валюты.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(); 
	Пока Выборка.Следующий() Цикл 
		 СоответствиеВалют.Вставить(Выборка.Наименование, Выборка.Ссылка);
	КонецЦикла;
		
	//1. Создать соединение
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	Соединение = Новый HTTPСоединение("www.nbrb.by", , , , , 30, ЗащищенноеСоединение);	
	
	АдресРесурса = СтрШаблон("/api/exrates/rates?periodicity=%1&ondate=%2", 
			        0, Формат(Объект.ДатаКурсов,"ДФ=yyyy-MM-dd"));  
	
        //2. Создать HTTP-запрос				
	Запрос = Новый HTTPЗапрос(АдресРесурса);
	
        //3. Отправить запрос серверу
	Попытка
		 Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос);
	Исключение
		 Сообщить("Не удалось отправить запрос, причина: " + ОписаниеОшибки());
		 Возврат;
	КонецПопытки;
		
	//4. Проверка кода состояния
	Если Ответ.КодСостояния = 200 Тогда 
	        ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);	
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина); 
		Для Каждого ДанныеКурса Из МассивДанных Цикл 
				Сообщить(СтрШаблон("1 %1 = %2 BYN за %3 ед.", 
							ДанныеКурса["Cur_Abbreviation"],
							ДанныеКурса["Cur_OfficialRate"],
							ДанныеКурса["Cur_Scale"]));
							
		Валюта = СоответствиеВалют[ДанныеКурса["Cur_Abbreviation"]];
		Если ЗначениеЗаполнено(Валюта) Тогда
								
		ЗаписьРегистра = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.Период = Объект.ДатаКурсов; 
		ЗаписьРегистра.Валюта = Валюта;
		ЗаписьРегистра.Курс = ДанныеКурса["Cur_OfficialRate"]; 
		ЗаписьРегистра.Записать();	
		КонецЕсли;	
		КонецЦикла;	
	Иначе
		Сообщить(СтрШаблон("НЕ удалось выполнить HTTP-запрос, код состояния; %1, ответ сервера %2", 
                                  Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку()));  
        КонецЕсли;
				
КонецПроцедуры
```
